apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.artifactory'
apply plugin: 'maven-publish'
group='OkHi'

def libraryGroupId = 'io.okhi.android'
def libraryArtifactId = 'background-geofencing'
def libraryCompileSdk = 33
def libraryTargetSdk = 33
def libraryMinSdk = 19
def libraryVersionCode = 1
def libraryVersion = '1.9.19'

android {
    compileSdkVersion libraryCompileSdk
    buildToolsVersion "30.0.3"

    defaultConfig {
        minSdkVersion libraryMinSdk
        targetSdkVersion libraryTargetSdk
        versionCode libraryVersionCode
        versionName libraryVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'androidx.appcompat:appcompat:1.6.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    implementation 'com.snappydb:snappydb-lib:0.5.2'
    api 'com.esotericsoftware.kryo:kryo:2.24.0'
    implementation 'com.google.android.gms:play-services-location:21.0.1'
    implementation ("com.squareup.okhttp3:okhttp:3.12.12"){
        force = true //API 19 support
    }
    implementation "androidx.work:work-runtime:2.7.1"
    implementation 'io.okhi.android:core:1.7.8'
}

publishing {
    publications {
        aar(MavenPublication) {
            groupId libraryGroupId
            version libraryVersion
            artifactId libraryArtifactId
            artifact("$buildDir/outputs/aar/android-${artifactId}-release.aar")
        }
    }
}

artifactory {
    contextUrl = artifactory_context_url
    publish {
        repository {
            repoKey = artifactory_repo_key
            username = artifactory_username
            password = artifactory_password
        }
        defaults {
            publications('aar')
            publishArtifacts = true

            properties = ['qa.level': 'basic', 'q.os': 'android', 'dev.team': 'core']
            publishPom = true
        }
    }
}